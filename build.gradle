plugins {
    id 'java'
    id 'application'
    id 'jacoco'
}

group = 'ru.clevertec.check'
version = '1.0-SNAPSHOT'
repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core:5.5.0'
    implementation 'org.postgresql:postgresql:42.7.3'
}

application {
    mainClass = 'ru.clevertec.check.CheckRunner'
}

jar {
    manifest {
        attributes 'Main-Class': 'ru.clevertec.check.CheckRunner'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        html.required.set(true)
        csv.required.set(false)
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.1
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification



